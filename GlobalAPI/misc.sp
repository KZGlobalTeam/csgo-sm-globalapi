bool ReadAPIKey()
{
    char fileToRead[PLATFORM_MAX_PATH] = "cfg/sourcemod/globalapi-key.cfg";
    if (!FileExists(fileToRead))
    {
        BuildPath(Path_SM, fileToRead, sizeof(fileToRead), "configs/globalapi-key.cfg");
    }

    // If key still does not exist, bail.
    if (!FileExists(fileToRead))
    {
        return false;
    }

    File file = OpenFile(fileToRead, "r");
    if (file == null)
    {
        LogError("Cannot open file %s", fileToRead);
        return false;
    }

    file.ReadLine(gC_apiKey, sizeof(gC_apiKey));
    delete file;

    TrimString(gC_apiKey);
    return !StrEqual(gC_apiKey, "");
}

void Initialize()
{
    gC_baseUrl = gCV_Staging.BoolValue ? GlobalAPI_Staging_BaseUrl : GlobalAPI_BaseUrl;

    gB_IsInit = true;
    Call_Global_OnInitialized();
}

bool FormatRequestUrl(char[] buffer, int maxlength, char[] endpoint)
{
    return Format(buffer, maxlength, "%s/%s", gC_baseUrl, endpoint) > 0;
}

void FormatPathParam(char[] buffer, int maxlength, char[] param, char[] value = "", int intValue = -1)
{
    char paramKey[128];
    Format(paramKey, sizeof(paramKey), "{%s}", param);

    if (intValue != -1)
    {
        char tempBuffer[64];
        IntToString(intValue, tempBuffer, sizeof(tempBuffer));
        ReplaceString(buffer, maxlength, paramKey, tempBuffer);
    }
    else
    {
        ReplaceString(buffer, maxlength, paramKey, value);
    }
}

// This could be failure, or just success with no response body
// We do not care. We call the forward with data as null anyways
void CallForward_NoResponse(GlobalAPIRequestData hData)
{
    any data = hData.Data;
    Handle hFwd = hData.Callback;

    CallForward(hFwd, null, hData, data);

    // Cleanup
    json_cleanup_and_delete(hData);

    delete hFwd;
}

GlobalAPIRequestData CreateRequestData(Handle plugin, Function callback, any data)
{
    GlobalAPIRequestData hData = new GlobalAPIRequestData(plugin);

    Handle hFwd = CreateForward(ET_Ignore, Param_Cell, Param_Cell, Param_Cell);

    if (callback != INVALID_FUNCTION)
    {
        AddToForward(hFwd, plugin, callback);
    }

    hData.Data = data;
    hData.Callback = hFwd;

    return hData;
}

// For requests that never started, nothing else will free these
void CleanupRequestData(GlobalAPIRequestData hData)
{
    Handle hFwd = hData.Callback;

    json_cleanup_and_delete(hData);

    delete hFwd;
}

void CallForward(Handle hFwd, JSON_Object hJson, GlobalAPIRequestData hData, any data)
{
    if (hFwd != null)
    {
        GlobalAPI_DebugMessage("Called a forward!");
        Call_StartForward(hFwd);
        Call_PushCell(hJson);
        Call_PushCell(hData);
        Call_PushCell(data);
        Call_Finish();
    }
}
